// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM8.hdl

/**
 * Memory of 8 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
    // Demux the address to the 8 load channels
    DMux8Way(
        in=load,
        sel=address,
        a=loada,
        b=loadb,
        c=loadc,
        d=loadd,
        e=loade,
        f=loadf,
        g=loadg,
        h=loadh
    );

    // The registers proper
    Register(in=in, load=loada, out=rega);
    Register(in=in, load=loadb, out=regb);
    Register(in=in, load=loadc, out=regc);
    Register(in=in, load=loadd, out=regd);
    Register(in=in, load=loade, out=rege);
    Register(in=in, load=loadf, out=regf);
    Register(in=in, load=loadg, out=regg);
    Register(in=in, load=loadh, out=regh);

    // The output logic is a simple muxer
    Mux8Way16(
        a=rega,
        b=regb,
        c=regc,
        d=regd,
        e=rege,
        f=regf,
        g=regg,
        h=regh,
        sel=address,
        out=out
    );
}
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    // From figure 5.9

    // Register A will be filled from either the instruction line or the ALU
    Mux16(a=instruction, b=alu, sel=instruction[15], out=regA);
    Not(in=instruction[15], out=aInstruction);
    Or(a=aInstruction, b=instruction[5], out=writeA);
    // Only read 15 bits of memory address
    Register(in=regA, in[15]=false, load=writeA, out[0..14]=addressM, out=A);

    // D register is loaded from the ALU when d2 is set
    Register(in=alu, load=instruction[4], out=D);

    // ALU uses A or M based on the a bit.
    Mux16(a=A, b=inM, sel=instruction[12], out=AM);
    // ALU control bits are mapped directly in C instructions.
    ALU(x=D, y=AM, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f =instruction[7], no=instruction[6], out=alu, out=outM, zr=zr, ng=ng);

    And(a=instruction[15], b=instruction[3], out=writeM);

    Jump(jump=instruction[0..2], ng=ng, zr=zr, jmp=jmpi);
    And(a=jmpi, b=instruction[15], out=jmp);
    PC(in=A, load=jmp, reset=reset, inc=true, out[0..14]=pc);
}